# Как узнать версию БСП?

## Вариант 1
Когда мы хотим подставить нужную версию БСП в обработку то используем функцию
```onec
Версия = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
```

## Вариант 2
Если просто хотим в коде посмотреть, то в функция ниже содержит все описание БСП, включая версию

```onec
ОбновлениеИнформационнойБазыБСП.ПриДобавленииПодсистемы()
```

# Полезные процедуры

```onec
ДополнительныеОтчетыИОбработки.ЗарегистрироватьОбработку(ОбъектСправочника, ПараметрыРегистрации)

ДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(Ссылка)

ДополнительныеОтчетыИОбработки.ВыполнитьКомандуИзФормыВнешнегоОбъекта(ИдентификаторКоманды, ПараметрыКоманды, ЭтаФорма)
```


# Вид доп. обработок

| Значение поля "Вид"       | Расположение команды                                                                                 |
| ------------------------- | ---------------------------------------------------------------------------------------------------- |
| ПечатнаяФорма             | В меню "Печать" на форме объекта или списка                                                          |
| ЗаполнениеОбъекта         | В меню "Заполнить" на форме объекта или списка                                                       |
| СозданиеСвязанныхОбъектов | В меню "Создать на основании" - "Создание связанных объектов.."    <br>  на форме объекта или списка |
| Отчет                     | В меню "Отчеты" на форме объекта или списка                                                          |
| ДополнительнаяОбработка   | В списке соответствующих подсистем в меню  <br> "Сервис" - "Дополнительные обработки"                |
| ДополнительныйОтчет       | В списке соответствующих подсистем в меню  <br> "Сервис" - "Дополнительные отчеты"                   |


# Как работают типы команд вызова?

- **ТипКомандыВызовСерверногоМетода**. Вызывает экспортную процедуру в модуле объекта. Данная процедура является стартовой точкой запуска.
Параметры для процедуры меняются в зависимости от вида доп. обработки.
- **ТипКомандыВызовКлиентскогоМетода** - Вызывает экспортную процедуру в модуле формы на клиенте. Так же для каждого вида обработки определяется набор параметров для процедуры
- **ТипКомандыОткрытиеФормы**. Просто открывает форму по умолчанию у текущий обработки или отчета


# Как выполнить отладку?

Просто добавить расширение с общим модулем, в котором будет нужная процедура вызываемая из обработки. 


# Полезные процедуры
Рекомендую обратить внимание на следующие процедуры и функции:

ДополнительныеОтчетыИОбработки.ЗарегистрироватьОбработку(ОбъектСправочника, ПараметрыРегистрации)
ДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(Ссылка)
ДополнительныеОтчетыИОбработки.ВыполнитьКомандуИзФормыВнешнегоОбъекта(ИдентификаторКоманды, ПараметрыКоманды, ЭтаФорма)

# Примеры

## Печатная форма

```onec
Функция СведенияОВнешнейОбработке() Экспорт      
	ВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия = "0.2";  
	ПараметрыРегистрации.Назначение.Добавить("Документ.ПриемНаРаботу");

	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Печать (Вызов серверного метода)'");
	Команда.Идентификатор = "КомандаПечатьВызовСерверногоМетода";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;         

	Возврат ПараметрыРегистрации; 

КонецФункции      

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	// Реализация логики команды.  
    ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КомандаПечатьВызовСерверногоМетода") Тогда 
		// Тут происходит магия с последующим открытием табличного документа
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
            КоллекцияПечатныхФорм,
            "КомандаПечатьВызовСерверногоМетода", 
            НСтр("ru='Доп соглашение'"),
            ПечатьДоговора(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
КонецПроцедуры

Функция ПечатьДоговора(МассивОбъектов, ОбъектыПечати)

	Макет = ПолучитьМакет("МакетПечатнойФормы");   
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.Вывести(ОбластьШапка);

	Возврат ТабДокумент;
	
КонецФункции
```


## Заполнение объектов

```onec
Функция СведенияОВнешнейОбработке() Экспорт      
	ВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиЗаполнениеОбъекта();
	ПараметрыРегистрации.Версия = "0.2";  
	ПараметрыРегистрации.Назначение.Добавить("Документ.НачислениеЗарплаты");

	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Заполнить чего-то там'");
	Команда.Идентификатор = "КомандаЗаполнитьВызовСерверногоМетода";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда.ПоказыватьОповещение = Истина;         

	Возврат ПараметрыРегистрации; 

КонецФункции      

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначения, ПараметрыВыполнения) Экспорт
	Для Каждого ОбъектНазначения из ОбъектыНазначения Цикл
		ОбъектЗаполнения = ОбъектНазначения.ПолучитьОбъект();
		ОбъектЗаполнения.Начисления.Очистить();
		ОбъектЗаполнения.НДФЛ.Очистить();
		ОбъектЗаполнения.Удержания.Очистить();
		
		ЗаполнитьЧемНибудьНужныеТЧ(ОбъектЗаполнения); 
		
		ОбъектЗаполнения.Записать();
	КонецЦикла; 
КонецПроцедуры

Процедура ЗаполнитьЧемНибудьНужныеТЧ(ОбъектЗаполнения)
	Сообщить("Заполнено все");
КонецПроцедуры

```

## Дополнительная обработка

```onec
Функция СведенияОВнешнейОбработке() Экспорт      
	ВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = "0.2";  
	ПараметрыРегистрации.Назначение.Добавить("Подсистемы.Кадры");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Обработать чего-то там'");
	Команда.Идентификатор = "КомандаОбработатьВызовСерверногоМетода";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;         
	
	Возврат ПараметрыРегистрации; 
	
КонецФункции      

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыВыполнения) Экспорт
	// Реализуем любую логику
	СоздатьНаграды();
КонецПроцедуры 

Процедура СоздатьНаграды()
	Для Сч = 1 по 10 Цикл
		ОбъектНаграда = Справочники.Награды.СоздатьЭлемент(); 
		ОбъектНаграда.Наименование = "Награда " + Сч;
		ОбъектНаграда.Записать();
	КонецЦикла;
КонецПроцедуры

```

## Дополнительный отчет

В данном примере используем стандартную форму и поведения обычного отчета. Все что делает наш отчет, так это просто запускается и открывает свою форму по умолчанию.

```onec
Функция СведенияОВнешнейОбработке() Экспорт      
	ВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = "0.2";  
	ПараметрыРегистрации.Назначение.Добавить("Подсистемы.Кадры");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Отчет чего-то там'");
	Команда.Идентификатор = "КомандаОткрытиеФормы";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Команда.ПоказыватьОповещение = Истина;         
	
	Возврат ПараметрыРегистрации; 
КонецФункции
```