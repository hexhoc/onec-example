////////////////////////////////////////////////
// МНОГОПОТОЧНАЯ ОБРАБОТКА
////////////////////////////////////////////////
&НаКлиенте
Процедура КомандаМногопоточнаяОбработка(Команда)  
	МногопоточнаяОбработкаНаСервере(4);
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура МногопоточнаяОбработкаНаСервере(КоличествоПотоков)  
	
	ВремяНачала = ТекущаяДата();
	КоличествоПотоков = 5;
	МассивЗаданий = Новый Массив;
	
	Для НомерПотока = 1 По КоличествоПотоков  Цикл
		Задание = ЗапуститьВыполнениеВФоне(НомерПотока);
		// добавляем задание в массив, чтобы потом отследить выполнение
		МассивЗаданий.Добавить(Задание);
	КонецЦикла; 
	
	ОжидатьЗавершения(МассивЗаданий);
	
	Длительность = ТекущаяДата()-ВремяНачала;
	Сообщить("Длительность: " + Длительность + "сек.");
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ЗапуститьВыполнениеВФоне(НомерПотока)  
	// Создаем временное хранилище для результата фонового задания
	// Передаем его адрес в ФоновоеЗадание
	АдресВХранилище = ПоместитьВоВременноеХранилище(Неопределено);
	
	// определяем массив параметров для процедуры
	НаборПараметров = Новый Массив;
	НаборПараметров.Добавить(НомерПотока);
	НаборПараметров.Добавить(АдресВХранилище);
	
	// запуск фонового задания
	Возврат ФоновыеЗадания.Выполнить("ОбщийМодуль1.ВыполнитьОперацию", НаборПараметров, АдресВХранилище);
КонецФункции

&НаСервереБезКонтекста
Процедура ОжидатьЗавершения(МассивЗаданий)  
	
	// Проверим результат выполнения фоновых заданий
	Если МассивЗаданий.Количество() > 0 Тогда
		ФоновыеЗадания.ОжидатьЗавершенияВыполнения(МассивЗаданий);
	КонецЕсли;                       
	
	// Получаем результат выполнения. В Задание.Ключ мы поместили АдресХранилища
	Для Каждого Задание Из МассивЗаданий Цикл
		Результат = ПолучитьИзВременногоХранилища(Задание.Ключ);
		Сообщить(Результат); 
	КонецЦикла;
	
КонецПроцедуры 


// ПРИМЕР ПРОЦЕДУРЫ ОБЩЕГО МОДУЛЯ
Процедура ВыполнитьОперацию(НомерПотока, АдресВХранилище) Экспорт          
	ВремяОкончания = ТекущаяДата() + 10;
	Пока ТекущаяДата() < ВремяОкончания Цикл
	КонецЦикла;
	
	Результат = "Номер потока " + НомерПотока + " выполнен";
	ЗаписьЖурналаРегистрации(
		"ВыполнитьОперацию(НомерПотока)", 
		УровеньЖурналаРегистрации.Информация, 
		Метаданные.ОбщиеМодули.ОбщийМодуль1,
		НомерПотока, Результат,);    
		
	ПоместитьВоВременноеХранилище(Результат, АдресВХранилище);
    
КонецПроцедуры