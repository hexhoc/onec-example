
////////////////////////////////////////////////
// ПОКАЗАТЬ ПРЕДУПРЕЖДЕНИЕ
////////////////////////////////////////////////
&НаКлиенте
Асинх Процедура КомандаПоказатьПредупреждение(Команда)  
	// 1 Предупреждение с ожиданием
	Обещание = ПредупреждениеАсинх("Первое");
	Результат = Ждать Обещание; // Тут зависаем пока пользователь не ответит
	
	// 2 Предупреждение без ожидания
	ПредупреждениеАсинх("Последнее"); // Тут не ждем ответа и идем выполняться дальше
	
	Сообщить("До свидания");
КонецПроцедуры

////////////////////////////////////////////////
// ЗАДАТЬ ВОПРОС
////////////////////////////////////////////////
&НаКлиенте
Асинх Процедура КомандаЗадатьВопрос(Команда)
	
	Обещание = ВопросАсинх("Купить слона", РежимДиалогаВопрос.ДаНет);
	Результат = Ждать Обещание;
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сообщить("Слон куплен");
	Иначе 
		Сообщить("Слон НЕ куплен");
	КонецЕсли;    
	
КонецПроцедуры

////////////////////////////////////////////////
// ПОКАЗАТЬ ВЫБОР ИЗ СПИСКА ЗНАЧЕНИЙ
////////////////////////////////////////////////
&НаКлиенте
Асинх Процедура КомандаПоказатьВыборИзСписка(Команда)
	
	ТестовыйСписок = Новый СписокЗначений();
	ТестовыйСписок.Добавить("Тест1", "Тестовое значение 1");
	ТестовыйСписок.Добавить("Тест2", "Тестовое значение 2");
	ТестовыйСписок.Добавить("Тест3", "Тестовое значение 3");
	
	Обещание = ТестовыйСписок.ВыбратьЭлементАсинх("Выберите элемент");
	Результат = Ждать Обещание;
	
	Сообщить(Результат);
	
КонецПроцедуры 


////////////////////////////////////////////////
// НАЙТИ И ПОМЕСТИТЬ ФАЙЛЫ НА СЕРВЕРЕ
////////////////////////////////////////////////
&НаКлиенте
Асинх Процедура КомандаПоместитьФайлы(Команда)
	// Выполняем поиск файлов
	ПутьКПапкеСФайлами = "C:\Program Files (x86)\1cv8\conf";
	Обещание = НайтиФайлыАсинх(ПутьКПапкеСФайлами, "*.*", Ложь);
	Результат = Ждать Обещание;
	
	// Добавляем помещаемые файлы в массив
	ДобавляемыеФайлы = Новый Массив;
	Для каждого НайденныйФайл Из Результат Цикл
		ОписаниеПередаваемогоФайла = Новый ОписаниеПередаваемогоФайла;
		ОписаниеПередаваемогоФайла.Имя = НайденныйФайл.ПолноеИмя;
		ДобавляемыеФайлы.Добавить(ОписаниеПередаваемогоФайла);
	КонецЦикла; 
	
	// Помещаем файлы во временное хранилище на сервере
	Обещание = ПоместитьФайлыНаСерверАсинх(,,ДобавляемыеФайлы, ЭтаФорма.УникальныйИдентификатор);
	Результат = Ждать Обещание; 
	
	// Передаем адрес файла во временном хранилище на сервер и там обрабатываем его
	Для каждого ОписаниеПомещенногоФайла Из Результат Цикл
		ОбработатьФайлыНаСервере(ОписаниеПомещенногоФайла.Адрес);
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////
// ВЫБОР ФАЙЛА И ОБРАБОТКА ЕГО НА СЕРВЕРЕ
////////////////////////////////////////////////
&НаКлиенте
Асинх Процедура КомандаВыбораФайла(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	ДиалогВыбораФайла.ПолноеИмяФайла = "";
	ДиалогВыбораФайла.Фильтр = "txt (*.txt)|*.txt";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Каталог = "C:\";
	
	Обещания = ДиалогВыбораФайла.ВыбратьАсинх();
	ВыбранныеФайлы = Ждать Обещания;
	
	Если ВыбранныеФайлы.Количество() > 0 Тогда
		Обещание = ПоместитьФайлНаСерверАсинх(,,,ВыбранныеФайлы[0], ЭтаФорма.УникальныйИдентификатор);
		ОписаниеПомещенногоФайла = Ждать Обещание; 
		ОбработатьФайлыНаСервере(ОписаниеПомещенногоФайла.Адрес);
	КонецЕсли;

КонецПроцедуры