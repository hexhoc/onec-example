
# Описание

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товаров компания может оказывать дополнительные услуги, например, по доставке. 

И услуги, и товары указываются в одной табличной части.

Складской учет товаров не ведется. При проведении «Расходной накладной» при нехватке товара программа должна выдавать соответствующее предупреждение с указанием количества «нехватки» и не позволять проводить такой документ. 

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от текущего значения принятого на этот год в Учетной политике компании метода списания себестоимости (FIFO или LIFO). 

Обратите внимание – Учетная политика действует год. На следующий год метод списания может измениться. 

Необходимо построить отчеты по продажам товаров за период и остаткам товара на указанную дату.

**ПродажиТоваров**

| Номенклатура    | Кол-во | Себестоимость | Продажи  | Прибыль  |
| --------------- | ------ | ------------- | -------- | -------- |
| Куртка замшевая | 2,000  | 300,00        | 800,00   | 500      |
| Портсигар       | 1,000  | 50,00         | 200,00   | 150      |
| Доставка        | 1,000  |               | 1 000,00 | 1 000,00 |

**ОстаткиТоваров**

| Номенклатура    | Партия                                               | Кол-во | Стоимость |
| --------------- | ---------------------------------------------------- | ------ | --------- |
| Куртка замшевая |                                                      | 1,000  | 300,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 300,00    |
| Портсигар       |                                                      | 2,000  | 250,00    |
|                 | Приходная накладная 000000002 от 01.02.2025 12:00:00 | 1,000  | 100,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 150,00    |
| Итого           |                                                      | 3,000  | 550,00    |

# Метаданые

## Справочники

Номенклатура
- ЭтоУслуга
## Документы

ПриходнаяНакладная
- ТЧ СписокНоменклатуры
	- Номенклатура
	- Количество
	- Цена
	- Сумма
РасходнаяНакладная
- ТЧ СписокНоменклатуры
	- Номенклатура
	- Количество
	- Цена
	- Сумма
## Регистры

РегистрСведений.УчетнаяПолитика
- Измерения:
	- Периодичность: Год
	- Организация
- Ресурсы:
	- МетодСписанияСебестоимости

РегистрНакопления.ОстаткиНоменклатуры
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество

РегистрНакопления.Продажи
- Измерения:
	- Номенклатура
- Ресурсы:
	- Сумма
	- Себестоимость
	- Количество

РегистрНакопления.СебестоимостьНоменклатуры
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы:
	- Количество
	- Стоимость

# Реализация

## Приходная Накладная

```onec

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;   
	// регистр СебестоимостьНоменклатуры Приход
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл        
		ДвижениеОстаткиНоменклатуры(ТекСтрокаСписокНоменклатуры); 
		ДвижениеСебестоимостьНоменклатуры(ТекСтрокаСписокНоменклатуры);
	КонецЦикла;     
	
КонецПроцедуры   

Процедура ДвижениеОстаткиНоменклатуры(СтрокаСписокНоменклатуры) 
	// Остатки Номенклатуры	
	Движение = Движения.ОстаткиНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Номенклатура = СтрокаСписокНоменклатуры.Номенклатура;
	Движение.Количество = СтрокаСписокНоменклатуры.Количество;             
КонецПроцедуры  


Процедура ДвижениеСебестоимостьНоменклатуры(СтрокаСписокНоменклатуры) 
	// Себестоимость номенклатуры
	Движение = Движения.СебестоимостьНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Номенклатура = СтрокаСписокНоменклатуры.Номенклатура;
	Движение.Партия = ЭтотОбъект.Ссылка;
	Движение.Количество = СтрокаСписокНоменклатуры.Количество;
	Движение.Стоимость = СтрокаСписокНоменклатуры.Сумма;            
КонецПроцедуры  
```


## Расходная Накладная

```onec

Процедура ОбработкаПроведения(Отказ, РежимПроведения)  
	
	// Ставим Записывать = Истина, чтобы у нас успешно сработала строка Движения.Записать() для все отмеченных регистров
	// Эти галочки снимутся автоматически, если вызвать Движения.Записать() Обычная запись регистра их не снимает 
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;     

	// Очистить() для движения которые каким-то образом могли к нам попасть с формы
	Движения.ОстаткиНоменклатуры.Очистить();       
	Движения.СебестоимостьНоменклатуры.Очистить();
	
	// Записать() Записываем пустой набор записей. При перепроведении прошлые движения удаляются только
	// после записи новых движений, в конце транзакции. Но нам нужно чтобы прошлые движения удалились сразу
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.СебестоимостьНоменклатуры.Записать();
	
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементыБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементыБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементыБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементыБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	БлокировкаДанных.Заблокировать();
	
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(ТекущаяДатаСеанса());
	Если Не ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Документ не может быть преведён, поскольку не определён метод списания себестоимости!";
        Сообщение.Сообщить(); 

        Отказ = Истина;
        Возврат;
	КонецЕсли;

	ПорядокСортировкиПартий = ?(УчетнаяПолитика.МетодСписания = Перечисления.МетодСписанияСебестоимости.FIFO, "УБЫВ", "ВОЗР");
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;  
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = СтрШаблон(ТекстЗапросаРасходТоваров(), ПорядокСортировкиПартий);
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Неоперативный, МоментВремени(), Неопределено);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ОстатокСписания = ВыборкаНоменклатура.Количество;
		ОбщаяСтоимостьСписания = 0;
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ОстатокСписания = 0 Тогда
				Прервать;
			КонецЕсли; 
			
			КоличествоСписания = Мин(ВыборкаДетальныеЗаписи.КоличествоОстаток, ОстатокСписания); 
			СтоимостьСписания = 0;
			Если ВыборкаДетальныеЗаписи.КоличествоОстаток > 0 Тогда
				СтоимостьСписания = (ВыборкаДетальныеЗаписи.СтоимостьОстаток / ВыборкаДетальныеЗаписи.КоличествоОстаток) * КоличествоСписания
			КонецЕсли;
			
			ДвиженияСебестоимостьНоменклатуры(ВыборкаДетальныеЗаписи, КоличествоСписания, СтоимостьСписания);
			
			ОбщаяСтоимостьСписания = ОбщаяСтоимостьСписания + СтоимостьСписания;
			ОстатокСписания = ОстатокСписания - КоличествоСписания;
		КонецЦикла;   
		
		ДвиженияОстаткиНоменклатура(ВыборкаНоменклатура);
		ДвиженияПродажиТоваров(ВыборкаНоменклатура, ОбщаяСтоимостьСписания);
		
	КонецЦикла;
	
	Движения.Записать(); 
	
	Отказ = НЕ КонтрольОстатковПройден(МВТ, МоментВремени);
	
КонецПроцедуры  

Функция ТекстЗапросаРасходТоваров()
	Возврат "ВЫБРАТЬ
	        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	        |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	        |ПОМЕСТИТЬ ВТСписокНоменклатуры
	        |ИЗ
	        |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	        |ГДЕ
	        |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
			|   И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ЭтоУслуга = ЛОЖЬ
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	        |	СебестоимостьНоменклатурыОстатки.Партия КАК Партия,
	        |	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	        |	СебестоимостьНоменклатурыОстатки.СтоимостьОстаток КАК СтоимостьОстаток
	        |ПОМЕСТИТЬ ВТСебестоимостьНоменклатуры
	        |ИЗ
	        |	РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
	        |			&МоментВремени,
	        |			Номенклатура В
	        |				(ВЫБРАТЬ
	        |					ВТСписокНоменклатуры.Номенклатура
	        |				ИЗ
	        |					ВТСписокНоменклатуры)) КАК СебестоимостьНоменклатурыОстатки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТСписокНоменклатуры.Номенклатура КАК Номенклатура,
	        |	ВТСписокНоменклатуры.Количество КАК Количество,
	        |	ВТСписокНоменклатуры.Сумма КАК Сумма,
	        |	ВТСебестоимостьНоменклатуры.Партия КАК Партия,
	        |	ISNULL(ВТСебестоимостьНоменклатуры.КоличествоОстаток, 0) КАК КоличествоОстаток,
	        |	ISNULL(ВТСебестоимостьНоменклатуры.СтоимостьОстаток, 0) КАК СтоимостьОстаток
	        |ИЗ
	        |	ВТСписокНоменклатуры КАК ВТСписокНоменклатуры
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТСебестоимостьНоменклатуры КАК ВТСебестоимостьНоменклатуры
	        |		ПО ВТСписокНоменклатуры.Номенклатура = ВТСебестоимостьНоменклатуры.Номенклатура
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Партия %1
	        |ИТОГИ
	        |	МАКСИМУМ(Количество),
	        |	МАКСИМУМ(Сумма)
	        |ПО
	        |	Номенклатура";
КонецФункции

Процедура ДвиженияОстаткиНоменклатура(ВыборкаНоменклатуры)   
	
	Запись = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Запись.ВидДвижения = ВидДвиженияНакопления.Расход;    
	Запись.Период = Дата;
	Запись.Номенклатура = ВыборкаНоменклатуры.Номенклатура;
	Запись.Количество = ВыборкаНоменклатуры.Количество;    
	
КонецПроцедуры

Процедура ДвиженияПродажиТоваров(ВыборкаНоменклатуры, ОбщаяСтоимостьСписания)
	
	Запись = Движения.Продажи.Добавить();
	Запись.Период = Дата;
	Запись.Номенклатура = ВыборкаНоменклатуры.Номенклатура;
	Запись.Сумма= ВыборкаНоменклатуры.Сумма;    
	Запись.Себестоимость = ОбщаяСтоимостьСписания;    
	Запись.Количество = ВыборкаНоменклатуры.Количество;    
	
КонецПроцедуры

Процедура ДвиженияСебестоимостьНоменклатуры(ВыборкаДетальныеЗаписи, КоличествоСписания, СтоимостьСписания) 
	
	Запись = Движения.СебестоимостьНоменклатуры.Добавить();
	Запись.ВидДвижения = ВидДвиженияНакопления.Расход;
	Запись.Период = Дата;
	Запись.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
	Запись.Партия = ВыборкаДетальныеЗаписи.Партия;
	Запись.Количество = КоличествоСписания;
	Запись.Стоимость = СтоимостьСписания;
	
КонецПроцедуры

Функция КонтрольОстатковПройден(МВТ, МоментВремени)
	ПроверкаПройдена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, 
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТСписокНоменклатуры.Номенклатура
	|				ИЗ
	|					ВТСписокНоменклатуры)) КАК ОстаткиНоменклатурыОстатки
	|ГДЕ
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ПроверкаПройдена = Ложь; 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не хватает остатков по номенклатуре: %1", ВыборкаДетальныеЗаписи.Номенклатура);
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат ПроверкаПройдена
	
КонецФункции

```


## Продажи товаров

```onec
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот,
	ПродажиОбороты.СебестоимостьОборот КАК СебестоимостьОборот,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот - ПродажиОбороты.СебестоимостьОборот КАК Прибыль
ИЗ
	РегистрНакопления.Продажи.Обороты(, , Период, ) КАК ПродажиОбороты
```

## Остатки товаров

```onec
ВЫБРАТЬ
	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	СебестоимостьНоменклатурыОстатки.Партия КАК Партия,
	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	СебестоимостьНоменклатурыОстатки.СтоимостьОстаток КАК СтоимостьОстаток
ИЗ
	РегистрНакопления.СебестоимостьНоменклатуры.Остатки КАК СебестоимостьНоменклатурыОстатки
```