
# Описание

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товаров компания может оказывать дополнительные услуги, например, по доставке. 

И услуги, и товары указываются в одной табличной части. 

Складской учет товаров не ведется. 

При проведении «Расходной накладной» при нехватке товара программа должна выдавать соответствующее предупреждение с указанием количества «нехватки» и не позволять проводить такой документ. 

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от текущего значения принятого на этот год в Учетной политике компании метода списания себестоимости (FIFO или LIFO). 

Обратите внимание – Учетная политика действует год. На следующий год метод списания может измениться. 

В первую очередь должен списываться товар из указанной в шапке партии. В случае если товара по выбранной партии не хватает (или его нет), то товар списывается в соответствии с текущей Учетной политикой.

Необходимо построить отчеты по продажам товаров за период и остаткам товара на указанную дату.

**ПродажиТоваров**

| Номенклатура    | Кол-во | Себестоимость | Продажи  | Прибыль  |
| --------------- | ------ | ------------- | -------- | -------- |
| Куртка замшевая | 2,000  | 300,00        | 800,00   | 500      |
| Портсигар       | 1,000  | 50,00         | 200,00   | 150      |
| Доставка        | 1,000  |               | 1 000,00 | 1 000,00 |

**ОстаткиТоваров**

| Номенклатура    | Партия                                               | Кол-во | Стоимость |
| --------------- | ---------------------------------------------------- | ------ | --------- |
| Куртка замшевая |                                                      | 1,000  | 300,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 300,00    |
| Портсигар       |                                                      | 2,000  | 250,00    |
|                 | Приходная накладная 000000002 от 01.02.2025 12:00:00 | 1,000  | 100,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 150,00    |
| Итого           |                                                      | 3,000  | 550,00    |

# Метаданые

## Справочники

Номенклатура
- ЭтоУслуга

## Документы

ПриходнаяНакладная
- ТЧ СписокНоменклатуры
	- Номенклатура
	- Количество
	- Цена
	- Сумма
РасходнаяНакладная
- ДокументПоступления
- ТЧ СписокНоменклатуры
	- Номенклатура
	- Количество
	- Цена
	- Сумма
## Регистры

РегистрСведений.УчетнаяПолитика
- Измерения:
	- Периодичность: Год
- Ресурсы:
	- МетодСписания

РегистрНакопления.ОстаткиНоменклатуры
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество

РегистрНакопления.Продажи
- Измерения:
	- Номенклатура
- Ресурсы:
	- Сумма
	- Себестоимость
	- Количество

РегистрНакопления.СебестоимостьНоменклатуры
- Измерения:
	- Номенклатура
	- ДокументПоступления
- Ресурсы:
	- Количество
	- Стоимость

# Реализация

## Приходная Накладная

```onec

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;   
	// регистр СебестоимостьНоменклатуры Приход
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	
	Для Каждого ТекСтрокаТоварыИУслуги Из ТоварыИУслуги Цикл        
		ДвижениеОстаткиНоменклатуры(ТекСтрокаТоварыИУслуги); 
		ДвижениеСебестоимостьНоменклатуры(ТекСтрокаТоварыИУслуги);
	КонецЦикла;     
	
КонецПроцедуры   

Процедура ДвижениеОстаткиНоменклатуры(СтрокаТоварыИУслуги) 
	// Остатки Номенклатуры	
	Движение = Движения.ОстаткиНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Номенклатура = СтрокаТоварыИУслуги.Номенклатура;
	Движение.Количество = СтрокаТоварыИУслуги.Количество;             
КонецПроцедуры  


Процедура ДвижениеСебестоимостьНоменклатуры(СтрокаТоварыИУслуги) 
	// Себестоимость номенклатуры
	Движение = Движения.СебестоимостьНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Номенклатура = СтрокаТоварыИУслуги.Номенклатура;
	Движение.ДокументПоступления = ЭтотОбъект.Ссылка;
	Движение.Количество = СтрокаТоварыИУслуги.Количество;
	Движение.Стоимость = СтрокаТоварыИУслуги.Сумма;            
КонецПроцедуры  
```


## Расходная Накладная

```onec

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.СебестоимостьНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.СебестоимостьНоменклатуры.Записать();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.ОстаткиНоменклатуры"); 
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	БлокировкаДанных.Заблокировать();
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени());
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени);
	ПорядокСписания = ?(УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО, "УБЫВ", "ВОЗР");
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон(ТекстЗапросаПартииТоваров(), ПорядокСписания); 
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПриоритетнаяПартия = ПолучитьОстаткиПриоритетнойПартии(МоментВремени, ДокументПоступления, МенеджерВТ);
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает остатка по номенклатуре: %1. Требуется: %2. В наличии: %3", 
			ВыборкаНоменклатура.Номенклатура, ВыборкаНоменклатура.Количество, ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		ИтогоСебестоимость = 0;       
		
		// Приоритетная партия
		СтрПриоритетнаяПартия = ТаблицаПриоритетнаяПартия.Найти(ВыборкаНоменклатура.Номенклатура, "Номенклатура");
		Если СтрПриоритетнаяПартия <> Неопределено Тогда
			КоличествоСписания = Мин(ОсталосьСписать, СтрПриоритетнаяПартия.КоличествоОстатокПартии); 
			СебестоимостьСписания = СтрПриоритетнаяПартия.СебестоимостьОстаток / СтрПриоритетнаяПартия.КоличествоОстатокПартии * КоличествоСписания;
			
			ДвиженияСебестоимостьНоменклатуры(СтрПриоритетнаяПартия, КоличествоСписания, СебестоимостьСписания);
			
			ОсталосьСписать = ОсталосьСписать - КоличествоСписания;
			ИтогоСебестоимость = ИтогоСебестоимость + СебестоимостьСписания;
		КонецЕсли;
		
		ВыборкаПартии = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаПартии.Следующий() Цикл             
			Если ОсталосьСписать = 0 Или ВыборкаНоменклатура.ЭтоУслуга = Истина Тогда
				Прервать;
			КонецЕсли;
			
			КоличествоСписания = Мин(ОсталосьСписать, ВыборкаПартии.КоличествоОстатокПартии); 
			СебестоимостьСписания = ВыборкаПартии.СебестоимостьОстаток / ВыборкаПартии.КоличествоОстатокПартии * КоличествоСписания;
			
			ДвиженияСебестоимостьНоменклатуры(ВыборкаПартии, КоличествоСписания, СебестоимостьСписания);
			
			ОсталосьСписать = ОсталосьСписать - КоличествоСписания;
			ИтогоСебестоимость = ИтогоСебестоимость + СебестоимостьСписания;
		КонецЦикла;
		
		ДвиженияПродажи(ВыборкаНоменклатура, ИтогоСебестоимость);		
		ДвиженияОстаткиНоменклатуры(ВыборкаНоменклатура);
	КонецЦикла;
	
	Движения.Записать();
	
КонецПроцедуры   

Процедура ДвиженияОстаткиНоменклатуры(ВыборкаНоменклатура)
	
	Движение = Движения.ОстаткиНоменклатуры.Добавить();
	Движение.ВидДвижения=ВидДвиженияНакопления.Расход;
	Движение.Период=Дата;
	Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
	Движение.Количество = ВыборкаНоменклатура.Количество;
	
КонецПроцедуры

Процедура ДвиженияСебестоимостьНоменклатуры(ВыборкаПартии, КоличествоСписания, Себестоимость) 
	
	Движение = Движения.СебестоимостьНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата; 
	Движение.Номенклатура = ВыборкаПартии.Номенклатура;
	Движение.ДокументПоступления = ВыборкаПартии.ДокументПоступления;
	Движение.Количество = КоличествоСписания;
	Движение.Себестоимость = Себестоимость; 
	
КонецПроцедуры

Процедура ДвиженияПродажи(ВыборкаНоменклатура, СебестоимостьСписания)
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
	Движение.Себестоимость = СебестоимостьСписания;
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
	Движение.Количество = ВыборкаНоменклатура.Количество;
КонецПроцедуры

Функция ТекстЗапросаПартииТоваров() 
	Возврат "ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
	|	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ЭтоУслуга КАК ЭтоУслуга
	|ПОМЕСТИТЬ ВТСписокНоменклатуры
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТОстаткиНоменклатуры
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТСписокНоменклатуры.Номенклатура
	|				ИЗ
	|					ВТСписокНоменклатуры)) КАК ОстаткиНоменклатурыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	СебестоимостьНоменклатурыОстатки.ДокументПоступления КАК ДокументПоступления,
	|	СебестоимостьНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток,
	|	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстатокПартии
	|ПОМЕСТИТЬ ВТСебестоимостьПартии
	|ИЗ
	|	РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТСписокНоменклатуры.Номенклатура
	|					ИЗ
	|						ВТСписокНоменклатуры)
	|				И НЕ ДокументПоступления = &ДокументПоступления) КАК СебестоимостьНоменклатурыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТСписокНоменклатуры.ЭтоУслуга КАК ЭтоУслуга,
	|	ВТСписокНоменклатуры.Количество КАК Количество,
	|	ВТСписокНоменклатуры.Сумма КАК Сумма,
	|	ВТОстаткиНоменклатуры.КоличествоОстаток КАК КоличествоОстаток,
	|	ВТСебестоимостьПартии.ДокументПоступления КАК ДокументПоступления,
	|	ВТСебестоимостьПартии.СебестоимостьОстаток КАК СебестоимостьОстаток,
	|	ВТСебестоимостьПартии.КоличествоОстатокПартии КАК КоличествоОстатокПартии
	|ИЗ
	|	ВТСписокНоменклатуры КАК ВТСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиНоменклатуры КАК ВТОстаткиНоменклатуры
	|		ПО ВТСписокНоменклатуры.Номенклатура = ВТОстаткиНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСебестоимостьПартии КАК ВТСебестоимостьПартии
	|		ПО ВТСписокНоменклатуры.Номенклатура = ВТСебестоимостьПартии.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументПоступления %1
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(Сумма),
	|	МАКСИМУМ(КоличествоОстаток),
	|	СУММА(СебестоимостьОстаток),
	|	СУММА(КоличествоОстатокПартии)
	|ПО
	|	Номенклатура";
КонецФункции

Функция ПолучитьОстаткиПриоритетнойПартии(МоментВремени, ДокументПоступления, МенеджерВТ)
	
	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	СебестоимостьНоменклатурыОстатки.ДокументПоступления КАК ДокументПоступления,
	|	СебестоимостьНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток,
	|	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстатокПартии
	|ИЗ
	|	РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТСписокНоменклатуры.Номенклатура
	|					ИЗ
	|						ВТСписокНоменклатуры)
	|				И ДокументПоступления = &ДокументПоступления) КАК СебестоимостьНоменклатурыОстатки";
	
	Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	ТаблицаПриоритетнаяПартия = Запрос.Выполнить().Выгрузить();
	ТаблицаПриоритетнаяПартия.Индексы.Добавить("Номенклатура");
	
	Возврат ТаблицаПриоритетнаяПартия;
	
КонецФункции

```


## Продажи товаров

```onec
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот,
	ПродажиОбороты.СебестоимостьОборот КАК СебестоимостьОборот,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот - ПродажиОбороты.СебестоимостьОборот КАК Прибыль
ИЗ
	РегистрНакопления.Продажи.Обороты(, , Период, ) КАК ПродажиОбороты
```

## Остатки товаров

```onec
ВЫБРАТЬ
	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	СебестоимостьНоменклатурыОстатки.Партия КАК Партия,
	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	СебестоимостьНоменклатурыОстатки.СтоимостьОстаток КАК СтоимостьОстаток
ИЗ
	РегистрНакопления.СебестоимостьНоменклатуры.Остатки КАК СебестоимостьНоменклатурыОстатки
```