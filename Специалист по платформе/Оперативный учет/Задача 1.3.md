# Описание

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товаров компания может оказывать дополнительные услуги, например, по доставке. 

И услуги, и товары указываются в одной табличной части. 

Складской учет товаров не ведется. 

При проведении «Расходной накладной» при нехватке товара программа должна выдавать соответствующее предупреждение с указанием количества «нехватки» и не позволять проводить такой документ. 

В документе «Расходная накладная» в табличной части для каждого товара пользователь указывает партию, которую необходимо списать. В том случае, если товара по указанной партии не хватает, документ не проводится, и выводится соответствующее сообщение о нехватке товара. 

Необходимо построить отчеты по анализу продаж товаров за период и остаткам товара на указанную дату.

**ПродажиТоваров**

| Номенклатура    | Кол-во | Себестоимость | Продажи  | Прибыль  | Интервал | Срок |
| --------------- | ------ | ------------- | -------- | -------- | -------- | ---- |
| Куртка замшевая | 2,000  | 300,00        | 800,00   | 500      | 10       | 20   |
| Портсигар       | 1,000  | 50,00         | 200,00   | 150      | разовая  | 50   |
| Доставка        | 1,000  |               | 1 000,00 | 1 000,00 | разовая  |      |

 **Прибыль рассчитывается как:** «Сумма продаж» – «Себестоимость» 

 **Интервал** – расчетный показатель, средний интервал отгрузок (в днях). Он рассчитывается как: «Дата первой отгрузки» – «Дата последней отгрузки» / «Количество отгрузок» В том случае, когда отгрузка была только одна, то в колонке «Интервал» выводится «разовая» (отгрузка). 

 **Срок** – расчетный показатель, срок последней отгрузки (в днях), определяющий, как давно прошла последняя отгрузка. Он рассчитывается как: «Конец периода отчета» – «Дата последнего документа отгрузки»

**ОстаткиТоваров**

| Номенклатура    | Партия                                               | Кол-во | Стоимость |
| --------------- | ---------------------------------------------------- | ------ | --------- |
| Куртка замшевая |                                                      | 1,000  | 300,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 300,00    |
| Портсигар       |                                                      | 2,000  | 250,00    |
|                 | Приходная накладная 000000002 от 01.02.2025 12:00:00 | 1,000  | 100,00    |
|                 | Приходная накладная 000000003 от 01.03.2025 18:06:25 | 1,000  | 150,00    |
| Итого           |                                                      | 3,000  | 550,00    |

# Реализация

## Расходная накладная
```onec

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	Движения.СебестоимостьНоменклатуры.Очистить();
	Движения.СебестоимостьНоменклатуры.Записать();
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.ДокументПоступления КАК ДокументПоступления,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТСписокНоменклатуры
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	СебестоимостьНоменклатурыОстатки.ДокументПоступления КАК ДокументПоступления,
		|	СебестоимостьНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток,
		|	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТСебестоимостьНоменклатуры
		|ИЗ
		|	РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
		|			&МоментВремени,
		|			(ДокументПоступления, Номенклатура) В
		|				(ВЫБРАТЬ
		|					ВТСписокНоменклатуры.ДокументПоступления,
		|					ВТСписокНоменклатуры.Номенклатура
		|				ИЗ
		|					ВТСписокНоменклатуры)) КАК СебестоимостьНоменклатурыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВТСписокНоменклатуры.ДокументПоступления КАК ДокументПоступления,
		|	ВТСписокНоменклатуры.Количество КАК Количество,
		|	ВТСписокНоменклатуры.Сумма КАК Сумма,
		|	ЕСТЬNULL(ВТСебестоимостьНоменклатуры.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	ЕСТЬNULL(ВТСебестоимостьНоменклатуры.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	ВТСписокНоменклатуры КАК ВТСписокНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСебестоимостьНоменклатуры КАК ВТСебестоимостьНоменклатуры
		|		ПО ВТСписокНоменклатуры.Номенклатура = ВТСебестоимостьНоменклатуры.Номенклатура
		|			И ВТСписокНоменклатуры.ДокументПоступления = ВТСебестоимостьНоменклатуры.ДокументПоступления";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1, необходимо: %2, осталось: %3", 
				Выборка.Номенклатура, Выборка.Количество, Выборка.КоличествоОстаток);
			Сообщение.Сообщить();
			Отказ = Истина;
			Прервать;
		КонецЕсли; 
		
		ДвижениеСебестоимостьНоменклатуры(Выборка);
		ДвижениеПродажи(Выборка)
	КонецЦикла;
	
	Движения.Записать();
	
КонецПроцедуры    

Процедура ДвижениеСебестоимостьНоменклатуры(Выборка)  
	Движение = Движения.СебестоимостьНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.ДокументПоступления = Выборка.ДокументПоступления;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Количество = Выборка.Количество;
	Движение.Себестоимость = Выборка.СебестоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
КонецПроцедуры

Процедура ДвижениеПродажи(Выборка)
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Сумма = Выборка.Сумма;
	Движение.Себестоимость = Выборка.СебестоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
	Движение.Количество = Выборка.Количество;
КонецПроцедуры

```

## Отчет Продажи

```onec
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.СуммаОборот КАК СуммаОборот,
	ПродажиОбороты.СебестоимостьОборот КАК СебестоимостьОборот,
	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	ПродажиОбороты.СуммаОборот - ПродажиОбороты.СебестоимостьОборот КАК ПрибыльОборот
ПОМЕСТИТЬ ВТПродажи
ИЗ
	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ПродажиОбороты
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Продажи.Номенклатура КАК Номенклатура,
	МАКСИМУМ(Продажи.Период) КАК ДатаПоследнейОтгрузки,
	МИНИМУМ(Продажи.Период) КАК ДатаПервойОтгрузки,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Продажи.Регистратор) КАК КоличествоОтгрузок
ПОМЕСТИТЬ ВТОтгрузки
ИЗ
	РегистрНакопления.Продажи КАК Продажи
ГДЕ
	Продажи.Номенклатура В
			(ВЫБРАТЬ
				ВТПродажи.Номенклатура
			ИЗ
				ВТПродажи)
	И Продажи.Период МЕЖДУ &НачалоПериода И &КонецПериода

СГРУППИРОВАТЬ ПО
	Продажи.Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТПродажи.Номенклатура КАК Номенклатура,
	ВТПродажи.СуммаОборот КАК СуммаОборот,
	ВТПродажи.СебестоимостьОборот КАК СебестоимостьОборот,
	ВТПродажи.КоличествоОборот КАК КоличествоОборот,
	ВТПродажи.ПрибыльОборот КАК ПрибыльОборот,
	РАЗНОСТЬДАТ(ВТОтгрузки.ДатаПервойОтгрузки, ВТОтгрузки.ДатаПоследнейОтгрузки, ДЕНЬ) / ВТОтгрузки.КоличествоОтгрузок КАК Интервал,
	РАЗНОСТЬДАТ(ВТОтгрузки.ДатаПоследнейОтгрузки, &КонецПериода, ДЕНЬ) КАК Срок
ИЗ
	ВТПродажи КАК ВТПродажи
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтгрузки КАК ВТОтгрузки
		ПО ВТПродажи.Номенклатура = ВТОтгрузки.Номенклатура
```