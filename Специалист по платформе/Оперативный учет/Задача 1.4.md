Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». 

Складской учет товаров не ведется. 

При проведении «Расходной накладной» при нехватке товара программа должна выдавать соответствующее предупреждение с указанием количества «нехватки» и не позволять проводить такой документ. 

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от текущего значения принятого на этот год в Учетной политике компании метода списания себестоимости (FIFO или LIFO) и указанной в документе (в табличной части документа) партии. 

Обратите внимание – Учетная политика действует год. На следующий год метод списания может измениться. 

В первую очередь должен списываться товар из указанной в табличной части партии. В случае если товара по выбранной партии не хватает (или его нет), то товар списывается в соответствии с текущей Учетной политикой. 

Необходимо построить отчеты по продажам товаров за период и остаткам товара на указанную дату.

# Реализация

## Расходная накладная
```onec

Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	ИнициализироватьДвижения();    
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Неоперативный, МоментВремени(), Неопределено);
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени);
	ПорядокСписания = ?(УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО, "ВОЗР", "УБЫВ");
		
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон(ТекстЗапросаСписаниеСебестоимости(), ПорядокСписания);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На хватает остаток по номенклатуре %1, нужно %2, осталось %3",
				ВыборкаНоменклатура.Номенклатура, ВыборкаНоменклатура.Количество, ВыборкаНоменклатура.КоличествоОстаток);
			Отказ = Истина;
			Прервать;
		КонецЕсли; 
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		ОбщаяСебестоимость = 0;   
		ПриоритетнаяПартия = Истина;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл      
			// Списание приоритетной партии
			Если ПриоритетнаяПартия Тогда
				КоличествоСписания = Мин(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстатокПриоритет);   
				СебестоимостьСписания = 0;
				Если КоличествоСписания > 0 Тогда
					СебестоимостьСписания = ВыборкаДетальныеЗаписи.СебестоимостьОстатокПриоритет / ВыборкаДетальныеЗаписи.КоличествоОстатокПриоритет * КоличествоСписания;
				КонецЕсли;
				
				ДвижениеСебестоимостьНоменклатуры(ВыборкаДетальныеЗаписи.ДокументПоступленияПриоритет, ВыборкаДетальныеЗаписи.Номенклатура, КоличествоСписания, СебестоимостьСписания); 
				
				ОсталосьСписать = ОсталосьСписать - КоличествоСписания;
				ОбщаяСебестоимость = ОбщаяСебестоимость + СебестоимостьСписания;  
				ПриоритетнаяПартия = Ложь;
			КонецЕсли;
			
			Если ОсталосьСписать <= 0 Тогда
				Прервать;
			КонецЕсли;  
			
			// Списание оставшихся партий
			КоличествоСписания = Мин(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);   
			СебестоимостьСписания = 0;
			Если КоличествоСписания > 0 Тогда
				СебестоимостьСписания = ВыборкаДетальныеЗаписи.СебестоимостьОстаток/ ВыборкаДетальныеЗаписи.КоличествоОстаток* КоличествоСписания;
			КонецЕсли;
			
			ДвижениеСебестоимостьНоменклатуры(ВыборкаДетальныеЗаписи.ДокументПоступления, ВыборкаДетальныеЗаписи.Номенклатура, КоличествоСписания, СебестоимостьСписания); 
			
			ОсталосьСписать = ОсталосьСписать - КоличествоСписания;
			ОбщаяСебестоимость = ОбщаяСебестоимость + СебестоимостьСписания
			
		КонецЦикла;
		
		ДвижениеПродажи(ВыборкаНоменклатура, ОбщаяСебестоимость);
	КонецЦикла; 
	
	Движения.Записать();
КонецПроцедуры    

Функция ТекстЗапросаСписаниеСебестоимости()
	Возврат "ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка КАК Ссылка,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.ДокументПоступления КАК ДокументПоступления,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТСписокНоменклатуры
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.ДокументПоступления,
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СебестоимостьНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	СебестоимостьНоменклатурыОстатки.ДокументПоступления КАК ДокументПоступления,
	|	СебестоимостьНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток,
	|	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТСебестоимостьНоменклатуры
	|ИЗ
	|	РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТСписокНоменклатуры.Номенклатура
	|				ИЗ
	|					ВТСписокНоменклатуры)) КАК СебестоимостьНоменклатурыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСписокНоменклатуры.Ссылка КАК Ссылка,
	|	ВТСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТСписокНоменклатуры.ДокументПоступления КАК ДокументПоступленияПриоритет,
	|	ВТСебестоимостьНоменклатуры.ДокументПоступления КАК ДокументПоступления,
	|	ВТСписокНоменклатуры.Количество КАК Количество,
	|	ВТСписокНоменклатуры.Сумма КАК Сумма,
	|	ЕСТЬNULL(ВТСебестоимостьНоменклатуры.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
	|	ЕСТЬNULL(ВТСебестоимостьНоменклатуры.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ВТСебестоимостьПриоритетнойПартии.СебестоимостьОстаток, 0) КАК СебестоимостьОстатокПриоритет,
	|	ЕСТЬNULL(ВТСебестоимостьПриоритетнойПартии.КоличествоОстаток, 0) КАК КоличествоОстатокПриоритет
	|ИЗ
	|	ВТСписокНоменклатуры КАК ВТСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСебестоимостьНоменклатуры КАК ВТСебестоимостьНоменклатуры
	|		ПО ВТСписокНоменклатуры.Номенклатура = ВТСебестоимостьНоменклатуры.Номенклатура
	|			И ВТСписокНоменклатуры.ДокументПоступления <> ВТСебестоимостьНоменклатуры.ДокументПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСебестоимостьНоменклатуры КАК ВТСебестоимостьПриоритетнойПартии
	|		ПО ВТСписокНоменклатуры.Номенклатура = ВТСебестоимостьПриоритетнойПартии.Номенклатура
	|			И ВТСписокНоменклатуры.ДокументПоступления = ВТСебестоимостьПриоритетнойПартии.ДокументПоступления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументПоступления %1
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(Сумма),
	|	СУММА(СебестоимостьОстаток),
	|	СУММА(КоличествоОстаток),
	|	МАКСИМУМ(СебестоимостьОстатокПриоритет),
	|	МАКСИМУМ(КоличествоОстатокПриоритет)
	|ПО
	|	Номенклатура";
КонецФункции

Процедура ИнициализироватьДвижения()
	Движения.СебестоимостьНоменклатуры.Очистить();
	Движения.СебестоимостьНоменклатуры.Записать();
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
КонецПроцедуры

Процедура ДвижениеСебестоимостьНоменклатуры(ДокументПоступления, Номенклатура, КоличествоСписания, СебестоимостьСписания)  
	Движение = Движения.СебестоимостьНоменклатуры.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.ДокументПоступления = ДокументПоступления;
	Движение.Номенклатура = Номенклатура;
	Движение.Количество = КоличествоСписания;
	Движение.Себестоимость = СебестоимостьСписания;
КонецПроцедуры

Процедура ДвижениеПродажи(Выборка, ОбщаяСебестоимость)
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Сумма = Выборка.Сумма;
	Движение.Себестоимость = ОбщаяСебестоимость;
	Движение.Количество = Выборка.Количество;
КонецПроцедуры

```