
// Создать и вывести таблицу значений на форму
&НаСервере
Процедура ДобавитьТаблицуЗначенийНаФорму()
	ТаблицаЗначений = СоздатьТаблицуЗначений();
	
	МассивРеквизитов = Новый Массив;
	// добавим таблицу: сначала саму таблицу, потом колонку.
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ТаблицаНаФорме", Новый ОписаниеТипов("ТаблицаЗначений")));
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаНаФорме"));
	КонецЦикла;    
	
	// добавим реквизиты на форму
	ИзменитьРеквизиты(МассивРеквизитов);        
	
	// добавим элементы формы
	ЭлементФормыТаблицы = Элементы.Добавить("ТаблицаНаФорме", Тип("ТаблицаФормы"));
	ЭлементФормыТаблицы.ПутьКДанным = "ТаблицаНаФорме";    
	
	// запретим менять положение строк и сами строки, отключим командную панель
	ЭлементФормыТаблицы.ИзменятьСоставСтрок = Ложь;
	ЭлементФормыТаблицы.ИзменятьПорядокСтрок = Ложь;
	ЭлементФормыТаблицы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;

	Для Каждого Колонка ИЗ ТаблицаЗначений.Колонки Цикл
		ЭлементКолонкиТаблицы = Элементы.Добавить("Колонка" + Колонка.Имя, Тип("ПолеФормы"), ЭлементФормыТаблицы);
		ЭлементКолонкиТаблицы.Вид = ВидПоляФормы.ПолеНадписи;
		ЭлементКолонкиТаблицы.ПутьКДанным = "ТаблицаНаФорме" + "." + Колонка.Имя;
		ЭлементКолонкиТаблицы.Заголовок = Колонка.Имя;
	КонецЦикла;

	// заполним таблицу
	ЗначениеВРеквизитФормы(ТаблицаЗначений, "ТаблицаНаФорме");
КонецПроцедуры

// Добавить на форму в таблицу значений новую колонку
&НаСервере
Процедура ДобавитьКолонкуВТаблицуНаФорме(ИмяКолонки, ЗаголовокКолонки)
	// добавим реквизит
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяКолонки, Новый ОписаниеТипов("Строка"), "ТаблицаНаФорме", ЗаголовокКолонки, Ложь));
	ИзменитьРеквизиты( МассивРеквизитов );
	
	ЭлементФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), Элементы.ТаблицаНаФорме);
	ЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементФормы.ПутьКДанным = "ТаблицаНаФорме." + ИмяКолонки; 
КонецПроцедуры

&НаСервере
Процедура ОтборСтрокаВТаблице()        
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("Колонка1", "10");
	Элементы.ТаблицаНаФорме.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураПоиска);
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформление()       
	ИмяПоля = "ТаблицаНаФорме.Колонка1";
	ЗначениеДляОтбора = "Значение 10";
	
	УсловноеОформление.Элементы.Очистить(); 
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();  
	ЭлементОформления.Использование = Истина;
	
	// Настройка оформляемого поля
	Поля = ЭлементОформления.Поля.Элементы.Добавить();
	Поля.Использование = Истина;
	Поля.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНаФормеКолонка1");
	Поля = ЭлементОформления.Поля.Элементы.Добавить();
	Поля.Использование = Истина;
   
	// Создаем условие отбора
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля); // имя поля
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; // либо задаем свой
	ЭлементОтбора.ПравоеЗначение = ЗначениеДляОтбора;
	ЭлементОтбора.Использование = Истина;  
	
	// Установка цвета оформления
	Оформление = ЭлементОформления.Оформление;
	Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЦветФона"), WebЦвета.ЛососьСветлый); 
	
КонецПроцедуры  


